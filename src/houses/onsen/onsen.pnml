// Ground

        spriteset (spr_onsen_h_ground_norm, "src/houses/onsen/gfx/ground_norm.png") {template_ground_norm(80,1)}
        spriteset (spr_onsen_h_ground_snow, "src/houses/onsen/gfx/ground_snow.png") {template_ground_norm(80,1)}

// Buildings

    // L
        spriteset (spr_onsen_h_L_norm, "src/houses/onsen/gfx/norm.png") {template_house_norm(80,1)}
        spriteset (spr_onsen_h_L_snow, "src/houses/onsen/gfx/snow.png") {template_house_norm(80,1)}


// Spritelayouts for onsen

// H variant | L level | Norm
    spritelayout sprlay_onsen_h_L_norm {
        ground {
            sprite:        spr_onsen_h_ground_norm(LOAD_TEMP(2));
        }
        building {
            sprite:        spr_onsen_h_L_norm (construction_state);
            recolour_mode: RECOLOUR_REMAP;
            palette:       recolour_remap + LOAD_TEMP(1);
        }
    }

// H variant | L level | Snow
    spritelayout sprlay_onsen_h_L_snow {
        ground {
            sprite:        spr_onsen_h_ground_snow(LOAD_TEMP(2));
        }
        building {
            sprite:        spr_onsen_h_L_snow (construction_state);
            recolour_mode: RECOLOUR_REMAP;
            palette:       recolour_remap + LOAD_TEMP(1);
        }
    }


// Random Switches
    // Building Colours
        switch (FEAT_HOUSES, SELF, onsen_build_clr_new, getbits(random_bits, 1, 3)) {
            0:  return 44;
            1:  return 50;
            2:  return 51;
            3:  return 52;
            4:  return 60;
            5:  return 72;
            6:  return 80;
                return 81;
        }
        switch (FEAT_HOUSES, SELF, onsen_build_clr_old, getbits(random_bits, 1, 3)) {
            0:  return 15;
            1:  return 16;
            2:  return 22;
            3:  return 23;
            4:  return 25;
            5:  return 62;
            6:  return 63;
                return 68;
        }


// Spritelayout Switches
    // h
        // L
            switch (FEAT_HOUSES,SELF, switch_onsen_sprites, [ 
                STORE_TEMP((current_year - age) < 1959 ? onsen_build_clr_old() : onsen_build_clr_new(),1), 
                STORE_TEMP(GroundAlley(), 2),
                terrain_type == TILETYPE_SNOW])
                {1: sprlay_onsen_h_L_snow; sprlay_onsen_h_L_norm; } 

// Item Block
    item(FEAT_HOUSES, item_onsen, 93, HOUSE_SIZE_1X1){
        property {
            substitute:              29;
            name:                    string(NAME_ONSEN);
            population:              100;
            accepted_cargos:         [[PASS, 4],[MAIL, 2]];
            local_authority_impact:  80;
            removal_cost_multiplier: 80;
            probability:             1;
            years_available:         [1870,0xFFFF];
            minimum_lifetime:        10;
            availability_mask:       [bitmask(TOWNZONE_INNER_SUBURB, TOWNZONE_OUTER_SUBURB, TOWNZONE_OUTSKIRT), bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, ABOVE_SNOWLINE, CLIMATE_TROPIC)];
            building_class:          3;
            building_flags:          bitmask(HOUSE_FLAG_NOT_SLOPED);
        }
        graphics {
            default:                 switch_onsen_sprites;
            construction_check:      switch_onsen_con_check;
            cargo_production:        func_produce(16,6);
        }
    }
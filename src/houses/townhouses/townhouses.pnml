// Ground

        spriteset (spr_townhouses_h_ground_norm, "src/houses/townhouses/gfx/ground_norm.png") {template_ground_norm(80,1)}
        spriteset (spr_townhouses_h_ground_snow, "src/houses/townhouses/gfx/ground_snow.png") {template_ground_norm(80,1)}

// Buildings

    // L
        spriteset (spr_townhouses_h_L_norm, "src/houses/townhouses/gfx/norm.png") {template_house_norm(80,1)}
        spriteset (spr_townhouses_h_L_snow, "src/houses/townhouses/gfx/snow.png") {template_house_norm(80,1)}


// Spritelayouts for townhouses

// H variant | L level | Norm
    spritelayout sprlay_townhouses_h_L_norm {
        ground {
            sprite:        spr_townhouses_h_ground_norm(3);
        }
        building {
            sprite:        spr_townhouses_h_L_norm (3);
            recolour_mode: RECOLOUR_REMAP;
            palette:       recolour_remap + LOAD_TEMP(1);
        }
    }

// H variant | L level | Snow
    spritelayout sprlay_townhouses_h_L_snow {
        ground {
            sprite:        spr_townhouses_h_ground_snow(3);
        }
        building {
            sprite:        spr_townhouses_h_L_snow (3);
            recolour_mode: RECOLOUR_REMAP;
            palette:       recolour_remap + LOAD_TEMP(1);
        }
    }


// Random Switches
    // Building Colours
        switch (FEAT_HOUSES, SELF, townhouses_build_clr_new, getbits(random_bits, 1, 3)) {
            0:  return 15;
            1:  return 31;
            2:  return 53;
            3:  return 62;
            4:  return 72;
            5:  return 82;
            6:  return 84;
                return 92;
        }


// Spritelayout Switches
    // h
        // L
            switch (FEAT_HOUSES,SELF, switch_townhouses_sprites, [ 
                STORE_TEMP(townhouses_build_clr_new(), 1),
                terrain_type == TILETYPE_SNOW])
                {1: sprlay_townhouses_h_L_snow; sprlay_townhouses_h_L_norm; } 

// Item Block
    item(FEAT_HOUSES, item_townhouses, 22, HOUSE_SIZE_1X1){
        property {
            substitute:              26;
            name:                    string(NAME_TOWNHOUSES);
            population:              20;
            accepted_cargos:         [[PASS, 4],[MAIL, 2]];
            local_authority_impact:  80;
            removal_cost_multiplier: 80;
            probability:             1;
            years_available:         [1945,0xFFFF];
            minimum_lifetime:        5;
            availability_mask:       [bitmask(TOWNZONE_OUTER_SUBURB , TOWNZONE_OUTSKIRT), bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, ABOVE_SNOWLINE, CLIMATE_TROPIC)];
            building_class:          0;
        }
        graphics {
            default:                 switch_townhouses_sprites;
            construction_check:      switch_house_con_check;
            cargo_production:        func_produce(2,1);
        }
    }
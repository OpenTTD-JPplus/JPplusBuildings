// Ground

        spriteset (spr_long_wooden_house_h_f_ground_norm, "src/houses/long_wooden_house/gfx/ground_norm.png") {template_ground_norm(80,1)}
        spriteset (spr_long_wooden_house_h_f_ground_snow, "src/houses/long_wooden_house/gfx/ground_snow.png") {template_ground_norm(80,1)}
        spriteset (spr_long_wooden_house_h_g_ground_norm, "src/houses/long_wooden_house/gfx/ground_norm.png") {template_ground_norm(80,2)}
        spriteset (spr_long_wooden_house_h_g_ground_snow, "src/houses/long_wooden_house/gfx/ground_snow.png") {template_ground_norm(80,2)}

// Buildings

    // F
        spriteset (spr_long_wooden_house_h_f_norm, "src/houses/long_wooden_house/gfx/norm.png") {template_house_norm(80,1)}
        spriteset (spr_long_wooden_house_h_f_snow, "src/houses/long_wooden_house/gfx/snow.png") {template_house_norm(80,1)}

    // G
        spriteset (spr_long_wooden_house_h_g_norm, "src/houses/long_wooden_house/gfx/norm.png") {template_house_norm(80,2)}
        spriteset (spr_long_wooden_house_h_g_snow, "src/houses/long_wooden_house/gfx/snow.png") {template_house_norm(80,2)}

// Trees

    // F
        spriteset (spr_long_wooden_house_h_f_trees, "src/houses/long_wooden_house/gfx/trees.png") {template_child_norm(80,1)}

    // G
        spriteset (spr_long_wooden_house_h_g_trees, "src/houses/long_wooden_house/gfx/trees.png") {template_child_norm(80,2)}


// Spritelayouts for long_wooden_house

// H variant | F level | Norm
    spritelayout sprlay_long_wooden_house_h_f_con_norm {
        ground {
            sprite:        spr_long_wooden_house_h_f_ground_norm(1);
        }
        building {
            sprite:        spr_long_wooden_house_h_f_norm (construction_state);
            recolour_mode: RECOLOUR_REMAP;
            palette:       recolour_remap + LOAD_TEMP(1);
        }
    }

// H variant | F level | Norm
    spritelayout sprlay_long_wooden_house_h_f_fin_norm {
        ground {
            sprite:        spr_long_wooden_house_h_f_ground_norm(1);
        }
        building {
            sprite:        spr_long_wooden_house_h_f_norm (construction_state);
            recolour_mode: RECOLOUR_REMAP;
            palette:       recolour_remap + LOAD_TEMP(1);
        }
        childsprite { // trees
            sprite:        spr_long_wooden_house_h_f_trees (LOAD_TEMP(10)); 
        }
    }

// H variant | F level | Snow
    spritelayout sprlay_long_wooden_house_h_f_con_snow {
        ground {
            sprite:        spr_long_wooden_house_h_f_ground_snow(1);
        }
        building {
            sprite:        spr_long_wooden_house_h_f_snow (construction_state);
            recolour_mode: RECOLOUR_REMAP;
            palette:       recolour_remap + LOAD_TEMP(1);
        }
    }

// H variant | F level | Snow
    spritelayout sprlay_long_wooden_house_h_f_fin_snow {
        ground {
            sprite:        spr_long_wooden_house_h_f_ground_snow(1);
        }
        building {
            sprite:        spr_long_wooden_house_h_f_snow (construction_state);
            recolour_mode: RECOLOUR_REMAP;
            palette:       recolour_remap + LOAD_TEMP(1);
        }
        childsprite { // trees
            sprite:        spr_long_wooden_house_h_f_trees (3); 
        }
    }

// H variant | G level | Norm
    spritelayout sprlay_long_wooden_house_h_g_con_norm {
        ground {
            sprite:        spr_long_wooden_house_h_g_ground_norm(1);
        }
        building {
            sprite:        spr_long_wooden_house_h_g_norm (construction_state);
            recolour_mode: RECOLOUR_REMAP;
            palette:       recolour_remap + LOAD_TEMP(1);
        }
    }

// H variant | G level | Norm
    spritelayout sprlay_long_wooden_house_h_g_fin_norm {
        ground {
            sprite:        spr_long_wooden_house_h_g_ground_norm(1);
        }
        building {
            sprite:        spr_long_wooden_house_h_g_norm (construction_state);
            recolour_mode: RECOLOUR_REMAP;
            palette:       recolour_remap + LOAD_TEMP(1);
        }
        childsprite { // trees
            sprite:        spr_long_wooden_house_h_g_trees (LOAD_TEMP(10)); 
        }
    }

// H variant | G level | Snow
    spritelayout sprlay_long_wooden_house_h_g_con_snow {
        ground {
            sprite:        spr_long_wooden_house_h_g_ground_snow(1);
        }
        building {
            sprite:        spr_long_wooden_house_h_g_snow (construction_state);
            recolour_mode: RECOLOUR_REMAP;
            palette:       recolour_remap + LOAD_TEMP(1);
        }
    }

// H variant | G level | Snow
    spritelayout sprlay_long_wooden_house_h_g_fin_snow {
        ground {
            sprite:        spr_long_wooden_house_h_g_ground_snow(1);
        }
        building {
            sprite:        spr_long_wooden_house_h_g_snow (construction_state);
            recolour_mode: RECOLOUR_REMAP;
            palette:       recolour_remap + LOAD_TEMP(1);
        }
        childsprite { // trees
            sprite:        spr_long_wooden_house_h_g_trees (3); 
        }
    }


// Construction State Switches

    switch(FEAT_HOUSES, SELF, sprlay_long_wooden_house_h_f_norm, construction_state) {
        3: sprlay_long_wooden_house_h_f_fin_norm;
           sprlay_long_wooden_house_h_f_con_norm;
    }
    switch(FEAT_HOUSES, SELF, sprlay_long_wooden_house_h_f_snow, construction_state) {
        3: sprlay_long_wooden_house_h_f_fin_snow;
           sprlay_long_wooden_house_h_f_con_snow;
    }
    switch(FEAT_HOUSES, SELF, sprlay_long_wooden_house_h_g_norm, construction_state) {
        3: sprlay_long_wooden_house_h_g_fin_norm;
           sprlay_long_wooden_house_h_g_con_norm;
    }
    switch(FEAT_HOUSES, SELF, sprlay_long_wooden_house_h_g_snow, construction_state) {
        3: sprlay_long_wooden_house_h_g_fin_snow;
           sprlay_long_wooden_house_h_g_con_snow;
    }

// Random Switches
    // Building Colours
        switch (FEAT_HOUSES, SELF, long_wooden_house_build_clr_new, getbits(random_bits, 1, 3)) {
            0:  return 15;
            1:  return 16;
            2:  return 22;
            3:  return 23;
            4:  return 62;
            5:  return 63;
            6:  return 68;
                return 92;
        }


// Spritelayout Switches
    // h
        // f
            switch (FEAT_HOUSES,SELF, switch_long_wooden_house_h_f_sprites, [ 
                STORE_TEMP(long_wooden_house_build_clr_new(), 1), 
                STORE_TEMP(Season(), 10),
                terrain_type == TILETYPE_SNOW])
                {1: sprlay_long_wooden_house_h_f_snow; sprlay_long_wooden_house_h_f_norm; } 
        // g
            switch (FEAT_HOUSES,SELF, switch_long_wooden_house_h_g_sprites, [ 
                STORE_TEMP(long_wooden_house_build_clr_new(), 1), 
                STORE_TEMP(Season(), 10),
                terrain_type == TILETYPE_SNOW])
                {1: sprlay_long_wooden_house_h_g_snow; sprlay_long_wooden_house_h_g_norm; } 


// Level Selection     
    switch (FEAT_HOUSES, SELF, switch_long_wooden_house_sprites, getbits(random_bits, 0, 1)) {         
        0: switch_long_wooden_house_h_f_sprites;             
           switch_long_wooden_house_h_g_sprites;     
    } 

// Item Block
    item(FEAT_HOUSES, item_long_wooden_house, 13, HOUSE_SIZE_1X1){
        property {
            substitute:              6;
            name:                    string(NAME_LONG_WOODEN_HOUSE);
            population:              20;
            accepted_cargos:         [[PASS, 4],[MAIL, 2]];
            local_authority_impact:  80;
            removal_cost_multiplier: 80;
            probability:             1;
            years_available:         [1700,1944];
            minimum_lifetime:        5;
            availability_mask:       [bitmask(TOWNZONE_OUTSKIRT, TOWNZONE_EDGE), bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, ABOVE_SNOWLINE, CLIMATE_TROPIC)];
            building_class:          0;
            building_flags:          bitmask(HOUSE_FLAG_NOT_SLOPED);
        }
        graphics {
            default:                 switch_long_wooden_house_sprites;
            cargo_production:        func_produce(1,1);
        }
    }